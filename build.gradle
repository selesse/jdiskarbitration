plugins {
    id 'java'
    id("com.vanniktech.maven.publish") version "0.35.0"
}

subprojects {
    apply plugin: 'java'

    group = 'com.selesse'
    version = '0.2.0'

    repositories {
        mavenCentral()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
}

configure(project(':lib')) {
    apply plugin: 'com.vanniktech.maven.publish'

    // Configure signing properties from environment variables (with underscores)
    // The op tool's dotenv parser doesn't support dots, so we use underscores and map them here
    ext."signing.keyId" = findProperty("signing_keyId")
    ext."signing.password" = findProperty("signing_password")
    ext."signing.secretKeyRingFile" = findProperty("signing_secretKeyRingFile")

    mavenPublishing {
        publishToMavenCentral()
        signAllPublications()

        coordinates('com.selesse', 'jdiskarbitration', version.toString())

        pom {
            name = 'jdiskarbitration'
            description = 'Java library for monitoring disk events on macOS using the DiskArbitration framework'
            inceptionYear = '2025'
            url = 'https://github.com/selesse/jdiskarbitration'

            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }

            developers {
                developer {
                    id = 'selesse'
                    name = 'Alex Selesse'
                    url = 'https://github.com/selesse/'
                }
            }

            scm {
                url = 'https://github.com/selesse/jdiskarbitration'
                connection = 'scm:git:git://github.com/selesse/jdiskarbitration.git'
                developerConnection = 'scm:git:ssh://git@github.com/selesse/jdiskarbitration.git'
            }
        }
    }
}
